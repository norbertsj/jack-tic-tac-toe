class Board {
    field Position p1;
    field Position p2;
    field Position p3;
    field Position p4;
    field Position p5;
    field Position p6;
    field Position p7;
    field Position p8;
    field Position p9;
    field BoardState state;

    static int H1;
    static int H2;
    static int H3;
    static int V1;
    static int V2;
    static int V3;
    static int D1;
    static int D2;

    constructor Board new() {
        let p1 = Position.new(180, 50);
        let p2 = Position.new(230, 50);
        let p3 = Position.new(280, 50);
        let p4 = Position.new(180, 100);
        let p5 = Position.new(230, 100);
        let p6 = Position.new(280, 100);
        let p7 = Position.new(180, 150);
        let p8 = Position.new(230, 150);
        let p9 = Position.new(280, 150);
        let H1 = 1;
        let H2 = 2;
        let H3 = 3;
        let V1 = 4;
        let V2 = 5;
        let V3 = 6;
        let D1 = 7;
        let D2 = 8;
        let state = BoardState.new();

        return this;
    }

    method void dispose() {
        do p1.dispose();
        do p2.dispose();
        do p3.dispose();
        do p4.dispose();
        do p5.dispose();
        do p6.dispose();
        do p7.dispose();
        do p8.dispose();
        do p9.dispose();
        do state.dispose();
        do Memory.deAlloc(this);

        return;
    }

    method Position getPosition(int input) {
        if (input = 1) { return p1; }
        if (input = 2) { return p2; }
        if (input = 3) { return p3; }
        if (input = 4) { return p4; }
        if (input = 5) { return p5; }
        if (input = 6) { return p6; }
        if (input = 7) { return p7; }
        if (input = 8) { return p8; }
        if (input = 9) { return p9; }

        return null;
    }

    method void fillPosition(int value, int input) {
        var Position position;
        let position = getPosition(input);
        do position.setValue(value);
        do updateState();

        return;
    }

    method boolean isValidPosition(int input) {
        var Position position;
        let position = getPosition(input);

        if (position = null) {
            return false;
        }

        return ~(position.hasValue());
    }

    method Array getPositionValues() {
        var Array values;
        let values = Array.new(9);

        let values[0] = p1.getValue();
        let values[1] = p2.getValue();
        let values[2] = p3.getValue();
        let values[3] = p4.getValue();
        let values[4] = p5.getValue();
        let values[5] = p6.getValue();
        let values[6] = p7.getValue();
        let values[7] = p8.getValue();
        let values[8] = p9.getValue();

        return values;
    }

    method boolean allPositionsFilled() {
        return (
            (p1.hasValue()) &
            (p2.hasValue()) &
            (p3.hasValue()) &
            (p4.hasValue()) &
            (p5.hasValue()) &
            (p6.hasValue()) &
            (p7.hasValue()) &
            (p8.hasValue()) &
            (p9.hasValue())
        );
    }

    method void updateState() {
        var Array v;
        let v = getPositionValues();

        do checkHorizontal(v);
        do checkVertical(v);
        do checkDiagonal(v);

        return;
    }

    method void resetState() {
        do state.init();

        return;
    }

    method void checkHorizontal(Array v) {
        if ((v[0] = 1) & (v[1] = 1) & (v[2] = 1)) { do state.update(true, H1); }
        if ((v[0] = 2) & (v[1] = 2) & (v[2] = 2)) { do state.update(true, H1); }
        if ((v[3] = 1) & (v[4] = 1) & (v[5] = 1)) { do state.update(true, H2); }
        if ((v[3] = 2) & (v[4] = 2) & (v[5] = 2)) { do state.update(true, H2); }
        if ((v[6] = 1) & (v[7] = 1) & (v[8] = 1)) { do state.update(true, H3); }
        if ((v[6] = 2) & (v[7] = 2) & (v[8] = 2)) { do state.update(true, H3); }

        return;
    }

    method void checkVertical(Array v) {
        if ((v[0] = 1) & (v[3] = 1) & (v[6] = 1)) { do state.update(true, V1); }
        if ((v[0] = 2) & (v[3] = 2) & (v[6] = 2)) { do state.update(true, V1); }
        if ((v[1] = 1) & (v[4] = 1) & (v[7] = 1)) { do state.update(true, V2); }
        if ((v[1] = 2) & (v[4] = 2) & (v[7] = 2)) { do state.update(true, V2); }
        if ((v[2] = 1) & (v[5] = 1) & (v[8] = 1)) { do state.update(true, V3); }
        if ((v[2] = 2) & (v[5] = 2) & (v[8] = 2)) { do state.update(true, V3); }

        return;
    }

    method void checkDiagonal(Array v) {
        if ((v[0] = 1) & (v[4] = 1) & (v[8] = 1)) { do state.update(true, D1); }
        if ((v[0] = 2) & (v[4] = 2) & (v[8] = 2)) { do state.update(true, D1); }
        if ((v[2] = 1) & (v[4] = 1) & (v[6] = 1)) { do state.update(true, D2); }
        if ((v[2] = 2) & (v[4] = 2) & (v[6] = 2)) { do state.update(true, D2); }

        return;
    }

    method boolean isMatch() {
        return state.getMatch();
    }

    method void draw() {
        do Screen.clearScreen();
        do Screen.setColor(true);
        do Screen.drawLine(180, 100, 330, 100);
        do Screen.drawLine(180, 150, 330, 150);
        do Screen.drawLine(230, 50, 230, 200);
        do Screen.drawLine(280, 50, 280, 200);

        do p1.draw();
        do p2.draw();
        do p3.draw();
        do p4.draw();
        do p5.draw();
        do p6.draw();
        do p7.draw();
        do p8.draw();
        do p9.draw();

        if (isMatch()) {
            do drawMatchLine(state.getLine());
        }

        return;
    }

    method void drawMatchLine(int line) {
        if (line = H1) {
            do drawAnimatedMatchLine(180, 75, 330, 75);
        }

        if (line = H2) {
            do drawAnimatedMatchLine(180, 125, 330, 125);
        }

        if (line = H3) {
            do drawAnimatedMatchLine(180, 175, 330, 175);
        }
        
        if (line = V1) {
            do drawAnimatedMatchLine(205, 50, 205, 200);
        }

        if (line = V2) {
            do drawAnimatedMatchLine(255, 50, 255, 200);
        }

        if (line = V3) {
            do drawAnimatedMatchLine(305, 50, 305, 200);
        }

        if (line = D1) {
            do drawAnimatedMatchLine(180, 50, 330, 200);
        }

        if (line = D2) {
            do drawAnimatedMatchLine(180, 200, 330, 50);
        }

        return;
    }

    method void drawAnimatedMatchLine(int x1, int y1, int x2, int y2) {
        var int xTemp1, xTemp2, yTemp1, yTemp2, pixelsPerStep, delayMs;
        var boolean xChanges, yChanges, condition;
        let xChanges = true;
        let yChanges = true;
        let pixelsPerStep = 10;
        let delayMs = 10;

        if (x1 = x2) {
            let xChanges = false;
        }

        if (y1 = y2) {
            let yChanges = false;
        }

        // in our case "x" always gets incremented, so lets init it here
        let xTemp1 = x1;
        let xTemp2 = x1 + pixelsPerStep;

        if (xChanges & yChanges) {
            // diagonal (if y2 < y1 then it is a D2 line, otherwise D1

            if (y2 < y1) {
                let yTemp1 = y1;
                let yTemp2 = y1 - pixelsPerStep;
                let condition = (xTemp2 < (x2 + 1)) & (yTemp2 > (y2 - 1));
            } else {
                let yTemp1 = y1;
                let yTemp2 = y1 + pixelsPerStep;
                let condition = (xTemp2 < (x2 + 1)) & (yTemp2 < (y2 + 1));
            }

            while (condition) {
                do Screen.drawLine(xTemp1, yTemp1 - 1, xTemp2, yTemp2 - 1);
                do Screen.drawLine(xTemp1, yTemp1, xTemp2, yTemp2);
                do Screen.drawLine(xTemp1, yTemp1 + 1, xTemp2, yTemp2 + 1);
                let xTemp1 = xTemp1 + pixelsPerStep;
                let xTemp2 = xTemp2 + pixelsPerStep;
                
                if (y2 < y1) {
                    let yTemp1 = yTemp1 - pixelsPerStep;
                    let yTemp2 = yTemp2 - pixelsPerStep;
                    let condition = (xTemp2 < (x2 + 1)) & (yTemp2 > (y2 - 1));
                } else {
                    let yTemp1 = yTemp1 + pixelsPerStep;
                    let yTemp2 = yTemp1 + pixelsPerStep;
                    let condition = (xTemp2 < (x2 + 1)) & (yTemp2 < (y2 + 1));
                }

                do Sys.wait(delayMs);
            }
        } else {
            if (xChanges) {
                // horizontal (assuming x1 < x2)
                while (xTemp2 < (x2 + 1)) {
                    do Screen.drawLine(xTemp1, y1 - 1, xTemp2, y1 - 1);
                    do Screen.drawLine(xTemp1, y1, xTemp2, y1);
                    do Screen.drawLine(xTemp1, y1 + 1, xTemp2, y1 + 1);
                    let xTemp1 = xTemp1 + pixelsPerStep;
                    let xTemp2 = xTemp2 + pixelsPerStep;
                    do Sys.wait(delayMs);
                }
            }

            if (yChanges) {
                // vertical (assuming y1 < y2)
                let yTemp1 = y1;
                let yTemp2 = y1 + pixelsPerStep;
                while (yTemp2 < (y2 + 1)) {
                    do Screen.drawLine(x1 - 1, yTemp1, x1 - 1, yTemp2);
                    do Screen.drawLine(x1, yTemp1, x1, yTemp2);
                    do Screen.drawLine(x1 + 1, yTemp1, x1 + 1, yTemp2);
                    let yTemp1 = yTemp1 + pixelsPerStep;
                    let yTemp2 = yTemp2 + pixelsPerStep;
                    do Sys.wait(delayMs);
                }
            }
        }

        return;
    }

    method void clear() {
        do p1.setValue(0);
        do p2.setValue(0);
        do p3.setValue(0);
        do p4.setValue(0);
        do p5.setValue(0);
        do p6.setValue(0);
        do p7.setValue(0);
        do p8.setValue(0);
        do p9.setValue(0);
        do resetState();

        return;
    }
}