class Game {
    field Board board;
    field String player;
    field boolean running;

    constructor Game new() {
        let board = Board.new();
        let running = false;
        let player = "X";
        return this;
    }

    method void dispose() {
        do board.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void run() {
        var int input;
        let running = true;

        while (running) {
            let input = readInput();
            do board.fillPosition(player, input);
            do board.draw();
            do switchPlayer();
        }

        return;
    }

    method int readInput() {
        var String readMessage;
        var int input;
        let input = 0;

        if (player.charAt(0) = 88) {
            let readMessage = "Move for X:";
        } else {
            let readMessage = "Move for O:";
        }

        // valid inputs: 1 - 9 (strings will be casted to 0)
        while ((input < 1) | (input > 9)) {
            do resetCursor();
            let input = Keyboard.readInt(readMessage); // need to figure out how to clear the line..
        }

        return input;
    }

    method void resetCursor() {
        do Output.moveCursor(20, 0);
        return;
    }

    method void switchPlayer() {
        if (player.charAt(0) = 88) {
            let player = "O";
        } else {
            let player = "X";
        }

        return;
    }
}